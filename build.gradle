plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE' apply false
    id 'com.avast.gradle.docker-compose' version '0.10.7' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.avast.gradle.docker-compose'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependencySet(group: 'org.mongodb', version: '4.0.0') {
                entry 'bson'
                entry 'mongodb-driver-core'
                entry 'mongodb-driver-sync'
                entry 'mongodb-driver-reactivestreams'
            }
        }
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }
}
