plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'com.github.node-gradle.node' version '2.2.4'
    id 'cz.habarta.typescript-generator' version '2.26.723'
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

node {
    download = true
}

generateTypeScript {
    classes = [
            'com.example.model.DateTime'
    ]
    jsonLibrary = 'jackson2'
    outputFile = 'build/generated/ts/types/models/index.d.ts'
    outputKind = 'global'
}

task compileTypeScript(type: NpmTask, dependsOn: npmInstall) {
    args = ['run', 'build']
}

compileTypeScript.dependsOn generateTypeScript
processResources.dependsOn compileTypeScript
